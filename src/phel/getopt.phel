(ns smeghead\getopt\getopt
  (:require smeghead\getopt\options)
  (:require smeghead\getopt\parse-argument)
  (:require phel\str))

(defstruct Result [args options errors])

(defn- gen-result [args options-defined]
  (loop [rest-args args
         arguments []
         opts {}
         errors []]
    (if (empty? rest-args)
      (Result arguments opts errors)
      (let [current-arg (first rest-args)
            current-option-defined (options/find-option options-defined current-arg)
            parsed (parse-argument/parse current-arg options-defined)]
        (cond
          (parse-argument/Stop? parsed)
            (recur
              []
              (concat arguments (rest rest-args))
              opts
              errors)
          (parse-argument/Error? parsed)
            (recur
              (rest rest-args)
              arguments
              opts
              (conj errors (parsed :message)))
          (parse-argument/Argument? parsed)
            (recur (rest rest-args) (conj arguments current-arg) opts errors)
          (not (current-option-defined :required))
            (recur
              (rest rest-args)
              arguments
              (assoc opts (keyword (parsed :name)) (parsed :value))
              errors)
          (current-option-defined :required)
            (let [value-empty (nil? (parsed :value))]
              (if (and value-empty (< (count rest-args) 2))
                (recur
                  (rest rest-args)
                  arguments
                  opts
                  (conj errors (format "Missing value option: \"%s\"" current-arg)))
                (if (parsed :consumed-options)
                  (let [next-arg (second rest-args)]
                    (if (str/starts-with? next-arg "-")
                     (recur
                       (rest rest-args)
                       arguments
                       opts
                       (conj errors (format "Missing value option: \"%s\"" current-arg)))
                     (recur
                      (rest (rest rest-args))
                      arguments
                      (assoc opts (keyword (parsed :name)) (second rest-args))
                      errors)))
                  (if value-empty
                    (recur
                      (rest (rest rest-args))
                      arguments
                      (assoc opts (keyword (parsed :name)) (second rest-args))
                      errors)
                    (recur
                      (rest rest-args)
                      arguments
                      (assoc opts (keyword (parsed :name)) (parsed :value))
                      errors))))))))))

(defn getopt [args options]
  (gen-result args (options/create options)))
